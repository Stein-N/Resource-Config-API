plugins {
    id 'ml-loader'
    id 'net.neoforged.moddev'
    id 'me.modmuss50.mod-publish-plugin'
}

sourceSets {
    testmod {
        runtimeClasspath += main.runtimeClasspath;
        compileClasspath += main.compileClasspath;
    }
}

dependencies {
    testmodImplementation sourceSets.main.output
}

neoForge {
    version = neoforge_version

    var at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) accessTransformers = [at.absolutePath]


    parchment {
        minecraftVersion = '1.21'
        mappingsVersion = parchment_version
    }

    runs {
        testClient {
            client()
            sourceSet = sourceSets.testmod
            ideName = "NeoForge Test Client (${project.path})"
            gameDirectory = project.file("runs/client")
        }
        testServer {
            server()
            sourceSet = sourceSets.testmod
            programArguments.add('nogui')
            ideName = "NeoForge Test Server (${project.path})"
            gameDirectory = project.file("runs/server")
        }
    }
    mods {
        "${mod_id}" { sourceSet sourceSets.main }
        "${mod_id}_test" { sourceSet sourceSets.testmod }
    }
}

publishMods {
    file = jar.archiveFile
    modLoaders.add("neoforge")
}