plugins {
    id 'ml-loader'
    id 'fabric-loom' version '1.6-SNAPSHOT'
}

configurations {
    includedLibs
}

repositories {
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}+${minecraft_version}"

    modImplementation("com.terraformersmc:modmenu:${mod_menu}")

    implementation group: 'com.electronwill.night-config', name: 'toml', version: "${night_config}"
    includedLibs group: 'com.electronwill.night-config', name: 'toml', version: "${night_config}"
}

sourceSets {
    testmod {
        runtimeClasspath += main.runtimeClasspath;
        compileClasspath += main.compileClasspath;
    }
}

loom {
    var aw = project(":common").file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) accessWidenerPath.set(aw)

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            programArgs('--username', 'Dev')
            runDir("runs/client")
        }
        testmodClient {
            client()
            setConfigName("Fabric Testmod Client")
            ideConfigGenerated(true)
            programArgs('--username', 'Dev')
            runDir("runs/client")
            source sourceSets.testmod
        }
    }
}

jar {
    from configurations.includedLibs.collect { it.isDirectory() ? it : zipTree(it) }
}

// Loader Specific Settings
publishMods {
    file = remapJar.archiveFile
    modLoaders.addAll("fabric", "quilt")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom.withXml {
                asNode().dependencies.each { node ->
                    node.replaceNode {}
                }
            }
        }
    }
}